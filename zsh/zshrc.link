export DOTRC=${HOME}/.dotrc
[[ -e "${DOTRC}" ]] && source ${DOTRC}
export DOTFILES=${DOTFILES:-${HOME}/.dotfiles}
export DOTLOCAL=${HOME}/.local
[[ ! -e ${DOTLOCAL} ]] && mkdir -p ${DOTLOCAL}
export ZSH=${DOTFILES}/zsh

# provides a distribution-specific configuration hierarchy
# (some things are common across platforms, some are Linux-specific,
# some Mac-specific, some maybe even RHEL-alike- or Debian-alike-specific)
# config.all.(sh|bash|zsh) == commonly sourced
# config.$OS.(sh|bash|zsh) == commonly sourced by OS
# config.$OS.$DISTRO.(sh|bash|zsh) == commonly sourced by distro (to deal with Linux distro differences)
OS=$(uname -s)
if [[ ${OS} == "Linux" ]]; then
    if [[ -e /etc/redhat-release ]]; then
        # redhat-derived (incl. CentOS)
        DISTRO="rh"
    elif [[ -e /etc/debian_version ]]; then
        # debian-derived (incl. Ubuntu)
        DISTRO="deb"
    fi
fi
flavors=( all ${OS:+${OS}} ${DISTRO:+${DISTRO}} )

configs=( config env path )
for config in ${ZSH}/${^configs}.${^flavors}.zsh(N) ;do
    source ${config}
done

if [[ -o interactive ]] ;then
    int_configs=( prompt aliases funcs comp zle )
    for config in ${ZSH}/${^int_configs}.${^flavors}.zsh(N) ;do
        source ${config}
    done
fi

[[ -e ${DOTLOCAL}/zshrc ]] && source ${DOTLOCAL}/zshrc

# vim: filetype=zsh:ts=4:sw=4:expandtab
